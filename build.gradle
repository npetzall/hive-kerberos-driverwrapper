plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'jacoco'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
compileJava.options.compilerArgs << "-Xlint:all"
compileTestJava.options.compilerArgs << "-Xlint:all"
compileTestJava.options.compilerArgs << "-Xlint:-processing"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.apache.hive:hive-jdbc:$hiveVersion:standalone") {
        transitive = false
    }
    compile("org.apache.hive:hive-exec:$hiveVersion") {
        transitive = false
    }
    compile("org.apache.hadoop:hadoop-core:$hadoopVersion") {
        exclude group: 'commons-cli'
        exclude group: 'xmlenc'
        exclude group: 'com.sun.jersey'
        exclude group: 'commons-io'
        exclude group: 'commons-net'
        exclude group: 'org.mortbay.jetty'
        exclude group: 'oro'
        exclude group: 'commons-codec'
        exclude group: 'org.apache.commons'
        exclude group: 'tomcat'
        exclude group: 'commons-el'
        exclude group: 'hsqldb'
        exclude group: 'net.java.dev.jets3t'
        exclude group: 'org.eclipse.jdt'
        exclude group: 'org.codehaus.jackson'
        exclude group: 'commons-httpclient'
    }
}

dependencies {
    testCompile "org.assertj:assertj-core:$assertjVersion"
    testCompile 'junit:junit:4.12'
    testCompile 'org.testcontainers:testcontainers:1.5.1'
}

jacoco {
    toolVersion = "$jacocoVersion"
}

jacocoTestReport.dependsOn test

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "$gradleWrapperVersion"
}
