plugins {
  id 'com.github.ben-manes.versions' version '0.15.0'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'jacoco'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
compileJava.options.compilerArgs << "-Xlint:all"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    testCompile "org.assertj:assertj-core:$assertjVersion"
    testCompile "org.spockframework:spock-core:$spockVersion"
    testCompile 'cglib:cglib-nodep:3.2.5'
    testCompile 'org.objenesis:objenesis:2.6'
    testCompile( 'com.athaydes:spock-reports:1.3.1' ) {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }
    testRuntime ('org.apache.hive:hive-jdbc:2.3.2:standalone'){
      transitive = false
    }
    testRuntime ('org.apache.hive:hive-exec:2.3.2'){
      transitive = false
    }
    testRuntime ('org.apache.hadoop:hadoop-core:1.2.1'){
      transitive = true
    }
}

jacoco {
    toolVersion = "$jacocoVersion"
}

jacocoTestReport.dependsOn test

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

task wrapper(type: Wrapper) {
    gradleVersion = "$gradleVersion"
}
